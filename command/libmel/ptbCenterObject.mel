/* ptbCenterObject.mel

Copyright 1998-2002, Dirk Bialluch
All rights reserved.

Author		: Dirk Bialluch

E-mail  	: DirkB@Mayaring.com
Homepage	: http://dirk-bialluch.mayaring.com

Creation Date:	22.05.1998
Last Update:	13.01.2002

*/

//
// Main procedure
//

global proc ptbCenterObject ()
{
	string $objList[] = `ptb_filterTransforms`;
	float $piv[3];
	int $bbMode = `optionMenu -q -select ptb12_bb`;
/*
	if( `checkBox -q -value ptb12_hierarchy` )
	{
		global string $ptb_hierarchy[];
		$ptb_hierarchy = {};

		ptb_listHierarchy $objList;
		$objList = stringArrayCatenate( $objList, $ptb_hierarchy );
	}
*/
	for ( $cnt = 0; $cnt < size( $objList ); $cnt++ )
	{
		float $bb[6] = `xform -q -os -boundingBox $objList[$cnt]`;
		float $diff[3] = {0,0,0};

		if( `radioButtonGrp -q -select ptb12_mode` == 1 )
			$piv = `xform -q -ws -sp $objList[$cnt]`;
		else
			$piv = `xform -q -ws -rp $objList[$cnt]`;

		if( `checkBox -q -value ptb12_xbox` )
		{
			if ($bbMode == 2)
				$diff[0] = $bb[0] - $piv[0];
			else if ($bbMode == 3)
				$diff[0] = $bb[3] - $piv[0];
			else
				$diff[0] = ($bb[0] + $bb[3]) / 2 - $piv[0];
		}
		if( `checkBox -q -value ptb12_ybox` )
		{
			if ($bbMode == 4)
				$diff[1] = $bb[1] - $piv[1];
			else if ($bbMode == 5)
				$diff[1] = $bb[4] - $piv[1];
			else
				$diff[1] = ($bb[1] + $bb[4]) / 2 - $piv[1];
		}
		if( `checkBox -q -value ptb12_zbox` )
		{
			if ($bbMode == 6)
				$diff[2] = $bb[2] - $piv[2];
			else if ($bbMode == 7)
				$diff[2] = $bb[5] - $piv[2];
			else
				$diff[2] = ($bb[2] + $bb[5]) / 2 - $piv[2];
		}

		if( `radioButtonGrp -q -select ptb12_mode` == 1 )
		{
			xform -r -ws -sp $diff[0] $diff[1] $diff[2] $objList[$cnt];
			if ( `checkBox -q -value ptb12_sticky` )
				xform -r -ws -rp $diff[0] $diff[1] $diff[2] $objList[$cnt];
		}
		else
		{
			xform -r -ws -rp $diff[0] $diff[1] $diff[2] $objList[$cnt];
			if ( `checkBox -q -value ptb12_sticky` )
				xform -r -ws -sp $diff[0] $diff[1] $diff[2] $objList[$cnt];
		}

		// move object to old pivot position
		xform -r -ws -translation ( -$diff[0] ) ( -$diff[1] ) ( -$diff[2] ) $objList[$cnt];
	}
}

// end of script
