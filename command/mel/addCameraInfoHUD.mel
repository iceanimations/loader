//register a procedure to display the info
global proc string cameraInfo()
{
	string $camera;
	string $camShapes[];
	string $hud_msg;

	// First get the panel that currently has focus
	string $panel = `getPanel -wf`;

	// Compare the panel's type to confirm it is a "modelPanel"
	if ( "modelPanel" == `getPanel -to $panel` )
	{
		// Get the name of the camera using the ‘modelEditor’ command
		$camera = `modelEditor -q -camera $panel`;
		$camShapes = `listRelatives -shapes $camera`;

		if(`attributeExists "in" $camShapes[0]`==1)
		{
			$in = `getAttr ($camera+".in")`;
			$out = `getAttr ($camera+".out")`;
			$currentFrame = `currentTime -q`;
			$frame = $currentFrame-$in;
	
			$hud_msg = $camera + " [" + $in + "-" + $out + "] Current Frame: " + $currentFrame + "  Frame: " + $frame;
		}

	}

	return $hud_msg;
}

global proc addCameraInfoHUD()
{
	

	if(`headsUpDisplay -ex HUDCameraInfo`)
		headsUpDisplay -rem HUDCameraInfo;

	headsUpDisplay -section          2
       	        -block            5
              	 -blockSize        "medium"
			 -ba 		      "center"
       	        -labelFontSize    "large"
              	 -command          "cameraInfo()"
	        -attachToRefresh
       	        HUDCameraInfo;

	print("Camera Info HUD is added!\n");
}

